plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
}

repositories {
    mavenCentral()
}

allure {
    report {  // allure-commandline
        version.set('2.32.2')
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.29.1") //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    constraints {
        add('implementation', 'org.junit.jupiter:junit-jupiter-api') {
            version { strictly '5.11.3' }
        }
        add('implementation', 'org.junit.platform:junit-platform-launcher') {
            version { strictly '1.11.3' }
        }
    }

    testImplementation(
            'io.qameta.allure:allure-selenide:2.29.1',
            'org.junit.jupiter:junit-jupiter:5.11.3',
            'org.aspectj:aspectjweaver:1.9.22.1',
            'com.codeborne:selenide:7.5.1',
            'org.aeonbits.owner:owner:1.0.12'
    )
}

test {
    useJUnitPlatform()
    systemProperties(System.getProperties())
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
}

tasks.register("smoke", Test) {

    useJUnitPlatform {
        includeTags("demoqa-tests")
    }
}